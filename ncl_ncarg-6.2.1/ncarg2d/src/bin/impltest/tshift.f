C
C	$Id: tshift.f,v 1.4 2008-07-27 00:59:03 haley Exp $
C                                                                      
C                Copyright (C)  2000
C        University Corporation for Atmospheric Research
C                All Rights Reserved
C
C The use of this Software is governed by a License Agreement.
C
      SUBROUTINE TSHIFT (MASK0,LENG0,MASK1,LENG1,IERAND,IWDLNG,INTLNG,
     1                                                         IOUT,IER)
      DIMENSION MASK0(LENG0),MASK1(LENG1)
      INTEGER ENDOFF,ENDFM1
      IER=-1
      WRITE (IOUT,121)
      IF (IWDLNG.LT.2 .OR. IWDLNG.GT.99) GOTO 107
      IF (INTLNG.LT.2 .OR. INTLNG.GT.IWDLNG) GOTO 107
      IF (LENG0.LT. IWDLNG .OR. LENG1.LT.IWDLNG) GOTO 107
      IF (IERAND.NE.0) GO TO 108
      IER = 1
      IF (INTLNG.LT.IWDLNG) WRITE (IOUT,122) INTLNG
      IWDM1=IWDLNG-1
           DO 101 I=1,INTLNG
           K=ISHIFT(MASK0(I),0)
           IF (K.NE.MASK0(I)) GO TO 110
  101      CONTINUE
      INTM1=INTLNG-1
           DO 102 I=1,INTM1
           K = ISHIFT(MASK0(1),I)
           IF (K.NE.MASK0(I+1)) GO TO 111
  102      CONTINUE
           DO 103 I=1,INTLNG
           K = ISHIFT(MASK0(I),IWDLNG)
           IF (K.NE.MASK0(I)) GO TO 113
  103      CONTINUE
           DO 104 I=1,INTM1
           K=ISHIFT(MASK0(IWDLNG),I)
           IF (K.NE.MASK0(I)) GO TO 115
  104      CONTINUE
           DO 105 I=1,INTM1
           ISUB=INTLNG-I
           K=IAND(ISHIFT(MASK0(INTLNG),-I),MASK1(ISUB))
           IF (K.NE.MASK0(ISUB)) GOTO 117
  105      CONTINUE
      ENDOFF=MIN0(IWDM1,INTLNG)
      ENDFM1=ENDOFF-1
           DO 106 I=1,ENDFM1
           ISUB=ENDOFF-I
           K=IAND(ISHIFT(MASK1(ENDOFF),-I),MASK1(ISUB))
           IF (K.NE.MASK1(ISUB)) GOTO 119
  106      CONTINUE
      WRITE (IOUT,123)
      IER = 0
      RETURN
  107 WRITE (IOUT,124) IWDLNG,INTLNG,LENG0,LENG1
      GO TO 109
  108 WRITE (IOUT,125)
      GO TO 109
  109 WRITE (IOUT,126)
      RETURN
  110 WRITE (IOUT,127)
      CALL MSKOUT(' ',IWDLNG-I,1,I-1,IOUT)
      WRITE (IOUT,128) K
      IM2=I-2
      IF (I.GT.1) WRITE (IOUT,129) IM2
      RETURN
  111 WRITE (IOUT,130) I
      CALL MSKOUT(' ',0,1,0,IOUT)
      WRITE (IOUT,128) K
      IF (I.LE.1) GOTO 112
      WRITE (IOUT,131)
      CALL MSKOUT(' ',0,1,0,IOUT)
      IM1=I-1
      WRITE (IOUT,132) IM1
  112 WRITE (IOUT,133)
      RETURN
  113 WRITE (IOUT,134) IWDLNG
      CALL MSKOUT(' ',IWDLNG-I,1,I-1,IOUT)
      WRITE (IOUT,128) K
      IF (I.LE.1) GOTO 114
      IM2=I-2
      WRITE (IOUT,135) IWDLNG,IM2
  114 WRITE (IOUT,131)
      CALL MSKOUT(' ',0,1,0,IOUT)
      WRITE (IOUT,132) INTM1
      WRITE (IOUT,133)
      RETURN
  115 WRITE (IOUT,134) I
      CALL MSKOUT(' ',0,1,IWDM1,IOUT)
      WRITE (IOUT,128) K
      IF (I.LE.1) GOTO 116
      WRITE (IOUT,136)
      CALL MSKOUT(' ',0,1,IWDM1,IOUT)
      IM1=I-1
      WRITE (IOUT,132) IM1
  116 WRITE (IOUT,131)
      CALL MSKOUT(' ',0,1,0,IOUT)
      WRITE (IOUT,132) INTM1
      WRITE (IOUT,135) IWDLNG,INTM1
      WRITE (IOUT,133)
      RETURN
  117 WRITE (IOUT,137)
      CALL MSKOUT(' ',IWDLNG-INTLNG,1,INTM1,IOUT)
      IGNORE=I+IWDLNG-INTLNG
      WRITE (IOUT,138) I,IGNORE
      WRITE (IOUT,128) K
      IF (I.LE.1) GOTO 118
      IGNORE=IWDLNG-INTLNG
      WRITE (IOUT,139) IGNORE
      CALL MSKOUT(' ',IGNORE,1,INTM1,IOUT)
      IM1=I-1
      WRITE (IOUT,132) IM1
  118 WRITE (IOUT,133)
      WRITE (IOUT,140)
      RETURN
  119 WRITE (IOUT,141) I
      CALL MSKOUT(' ',IWDLNG-ENDOFF,ENDOFF,0,IOUT)
      WRITE (IOUT,128) K
      IF (I.LE.1) GOTO 120
      WRITE (IOUT,142)
      CALL MSKOUT(' ',IWDLNG-ENDOFF,ENDOFF,0,IOUT)
      IM1=I-1
      WRITE (IOUT,143) IM1
  120 IGNORE=IWDLNG-INTLNG
      WRITE (IOUT,139) IGNORE
      CALL MSKOUT(' ',IGNORE,1,INTM1,IOUT)
      WRITE (IOUT,132) INTM1
      WRITE (IOUT,133)
      WRITE (IOUT,140)
      RETURN
  121 FORMAT ('1NOW TESTING ISHIFT.')
  122 FORMAT ('0WARNING.  BECAUSE YOUR FORTRAN DOES NOT USE AN ',
     1     'ENTIRE WORD WHEN'/
     2     ' COMPARING OR ASSIGNING INTEGERS, ISHIFT WILL BE TESTED ',
     3     'ONLY WITH THE'/
     4     ' LOWER ',I2,' BITS.')
  123 FORMAT ('0FINISHED TESTING ISHIFT.  ISHIFT APPARENTLY WORKS.')
  124 FORMAT ('0ERROR IN TEST PACKAGE DETECTED IN TSHIFT.  THE WORD ',
     1     'LENGTH MUST BE AT'/
     2     ' LEAST 2.  THE NUMBER OF BITS IN INTEGER COMPARISONS ',
     3     'AND ASSIGNMENTS'/
     4     ' MUST BE AT LEAST 2 AND .LE. WORD LENGTH.  AN I2 FORMAT ',
     5     'CONSTRAINS THE'/
     6     ' WORD LENGTH TO BE .LE. 99.  THE DIMENSION OF THE MASKS ',
     7     'MUST BE AT LEAST'/
     8     ' THE WORD LENGTH.'/
     9     ' IWDLNG=',I16,'.  INTLNG=',I16,'.'/
     +     ' LENG0=',I16,'.  LENG1=',I16,'.')
  125 FORMAT ('0THE ROUTINE IAND, WHICH IS REQUIRED TO TEST ISHIFT,',
     1        ' HAS NOT BEEN'/
     2        ' VERIFIED.')
  126 FORMAT ('0BECAUSE OF THIS, ISHIFT CANNOT BE TESTED.')
  127 FORMAT ('0ISHIFT FAILED TO MAKE A ZERO LENGTH SHIFT ON')
  128 FORMAT ('0IT RETURNED THE BINARY EQUIVALENT OF',I16,'.')
  129 FORMAT ('0ISHIFT DID SUCCESSFULLY PERFORM A ZERO LENGTH SHIFT ',
     1     'ON WORDS CONTAINING'/
     2     ' A 1 IN BIT K, K=0 TO ',I2,'.  (BIT 0 IS LOW ORDER.)')
  130 FORMAT ('0ISHIFT DID NOT SUCCESSFULLY PERFORM A LEFT SHIFT OF ',
     1     I2,' BITS ON')
  131 FORMAT ('0ISHIFT DID SUCCESSFULLY LEFT SHIFT')
  132 FORMAT (' K BITS, K=1 TO ',I2,'.')
  133 FORMAT ('0ISHIFT APPEARS TO ZERO SHIFT SUCCESSFULLY.')
  134 FORMAT ('0ISHIFT DID NOT SUCCESSFULLY DO A LEFT CIRCULAR SHIFT ',
     1     'OF ',I2,' BITS ON')
  135 FORMAT ('0ISHIFT DID SUCCESSFULLY PERFORM A LEFT CIRCULAR SHIFT',
     1     ' OF ',I2,' BITS ON'/
     2     ' WORDS CONTAINING A 1 IN BIT K, K=0 TO ',I2,'.  (BIT 0 ',
     3     'IS LOW ORDER.)')
  136 FORMAT ('0IOUT SUCCESSFULLY LEFT CIRCULARLY SHIFTED')
  137 FORMAT('0ISHIFT DID NOT SUCCESSFULLY RIGHT SHIFT')
  138 FORMAT (1X,I2,' BITS.  (THE UPPER ',I2,' BITS WERE IGNORED IN ',
     1     'THIS TEST.)')
  139 FORMAT ('0ISHIFT DID SUCCESSFULLY RIGHT SHIFT (IGNORING THE ',
     1     'UPPER K+',I2,' BITS)')
  140 FORMAT ('0AND ISHIFT ALSO APPEARS TO SUCCESSFULLY LEFT SHIFT.')
  141 FORMAT ('0ISHIFT DID NOT SUCCESSFULLY PERFORM A RIGHT END-OFF ',
     1     'SHIFT OF ',I2,' BITS ON')
  142 FORMAT ('0ISHIFT SUCCESSFULLY DID A RIGHT END-OFF SHIFT ON ')
  143 FORMAT (' OF K BITS, K=1 TO ',I2,'.')
      END
